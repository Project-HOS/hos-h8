; -------------------------------------------------------------------------- 
;  HOS 割り込み管理 アセンブリ部                                             
;                                                                            
;                                    Copyright (C) 1998-2002 by Project HOS  
; -------------------------------------------------------------------------- 

	;	.CPU     300HA

	;	.INCLUDE "extern.inc"


		.SECTION  P,CODE,ALIGN=2

; -----------------------------------------------
;       デフォルトの割り込みハンドラ 
; -----------------------------------------------
_int_default:
		RTE


; -----------------------------------------------
;            割り込みのトラップ
;   ER1 に呼び出しアドレスが入ってくる
; -----------------------------------------------
_int_trap:	
	; -------- レジスタ保存
		PUSH.L	ER0
		
	; -------- 多重割り込みの判別
		MOV.W	@_int_cnt,R0
		BNE	MUL_INT


	; ======== 最初の割り込み ========
		MOV.W	#1,R0
		MOV.W	R0,@_int_cnt
		
	; -------- システム状態の変更
		MOV.W	@_sysstat,R0
		BSET	#2,R0L
		MOV.W	R0,@_sysstat
		
	; -------- スタックポインタの入れ替え
		MOV.L	ER7,@_int_sp
		MOV.L	@___system_stack,ER7
		
	; -------- ハンドラ呼び出し
		JSR	@ER1
		
	; -------- 割り込み復帰
		LDC	#H'FF,CCR	; 割り込み禁止
		XOR.W	R0,R0
		MOV.W	R0,@_int_cnt
		
	; -------- スタックポインタ復帰
		MOV.L	@_int_sp,ER7
		
	; -------- システム状態変更
		MOV.W	@_sysstat,R0
		BCLR	#2,R0L
		MOV.W	R0,@_sysstat
		
	; -------- 遅延ディスパッチ
		MOV.W	@_sysstat,R0
		BNE	INT_RETURN
		MOV.W	@_dlydsp,R0
		BEQ	INT_RETURN
		JSR	@___tsk_dsp
		
	; -------- 復帰
INT_RETURN:	POP.L	ER0
		POP.L	ER1
		RTE


	; ======== 多重割り込み ========
MUL_INT:	
		INC.W	#1,R0
		MOV.W	R0,@_int_cnt
		
	; -------- 割り込みハンドラ呼び出し
		JSR	@ER1
		LDC	#H'FF,CCR	; 割り込み禁止
		
	; -------- 割り込みからの復帰
		MOV.W	@_int_cnt,R0
		DEC.W	#1,R0
		MOV.W	R0,@_int_cnt
		POP.L	ER0
		POP.L	ER1
		RTE


; -----------------------------------------------
;       割り込み処理データ
; -----------------------------------------------
		.SECTION  B,DATA,ALIGN=2
_int_sp:	.RES.L	1	; 割り込み時の ER7 保存用


;		.END
